apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.signity.tangerine"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode java.lang.Integer.parseInt("${version_code}")
        versionName "${version_name}"

        //These are the values for the authorities and account types.
        //Reference them in the java files with e.g. BuildConfig.ACCOUNT_TYPE.
        buildConfigField "String", "PROJECT_NUMBER", "\"${project_number}\""
        buildConfigField "String", "DATABASE_NAME", "\"${db_name}\""
        buildConfigField "String", "STORE_ID", "\"${store_id}\""
        buildConfigField "String", "STORE_TYPE", "\"${store_type}\""
        buildConfigField "int", "DB_VERSION", "${db_version}"
        buildConfigField "String", "NETWORK_URL", "\"${network_url}${store_id}/${api_version}\""
        buildConfigField "String", "LOGIN_WITH_EMAIL", "\"${login_with_email}\""

        //Reference them in .xml files.
        resValue "string", "app_name", "${app_name}"
        resValue "color", "ColorPrimary", "${color_primary}"
        resValue "color", "ColorPrimaryDark", "${color_primary_dark}"
        resValue "string", "facebook_app_id", "${facebook_app_id}"

    }
    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "map_api_key", "${map_key_dev}"
        }
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "map_api_key", "${map_key}"
        }
    }

}


ext {
    supportLibVersion = '27.1.0'
    playLibVersion = '15.0.1'
    okhttpLibVersion = '2.0.0'
    retrofitLibVersion = '1.9.0'
    picassoLibVersion = '2.5.2'
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"
    implementation "com.squareup.retrofit:retrofit:${retrofitLibVersion}"
    implementation "com.squareup.okhttp:okhttp:${okhttpLibVersion}"
    implementation "com.squareup.okhttp:okhttp-urlconnection:${okhttpLibVersion}"
    implementation "com.google.android.gms:play-services-maps:${playLibVersion}"
    implementation "com.google.android.gms:play-services-location:${playLibVersion}"
    implementation "com.google.android.gms:play-services-places:${playLibVersion}"
    implementation "com.google.android.gms:play-services-auth:${playLibVersion}"
    implementation 'com.google.android.gms:play-services-analytics:16.0.1'


    implementation "com.squareup.picasso:picasso:${picassoLibVersion}"
    implementation 'com.ToxicBakery.viewpager.transforms:view-pager-transforms:1.2.32@aar'

    implementation 'com.google.firebase:firebase-messaging:17.0.0'
    implementation 'com.facebook.android:facebook-android-sdk:4.1.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
}
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
